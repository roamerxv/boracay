buildscript {
    repositories {
        jcenter()

    }
    dependencies {
        // 这是 上传 jar 文件到 jcenter 实用的 插件
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3"
        // -----

        // 这是版本检测用的插件
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.15.0'
        // -----
    }
}
/* 这里定义一个 bintray 的插件 */
apply plugin: "com.jfrog.bintray"
/* 调用方法 gradle bintrayUpload */

/* 这里定义一个检查 dependency 版本的插件 begin*/
apply plugin: 'com.github.ben-manes.versions'
/* 调用方法 gradle dependencyUpdates -Drevision=release */

group 'pers.roamer'
version '1.0.1'
description = 'Boracay WEB 项目实用框架'


allprojects {
    repositories {
        jcenter()
        mavenLocal()
        mavenCentral()

    }
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'java'
}


sourceCompatibility = 1.8


tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"  // 编译时增加 UTF-8 参数
}

def projectName = "Boracay"
def projectDescription =  "Boracay WEB 应用框架"
def gitUrl = "https://github.com/roamerxv/boracay.git"
def sourceUrl = "https://github.com/roamerxv/boracay"


ext {
    springVersion = "4.3.9.RELEASE"
    springJpaVersion = "1.11.4.RELEASE"
    aspectjVersion = "1.8.10"
    log4jVersion = "2.8.2"
    apacheOroVersion = "2.0.8"
    jacksonVersion = "2.9.0.pr4"
    fastJsonVersion = "1.2.33"
    javaxServletAPIVersion = "4.0.0-b07"
    mysqlJavaConnectorVersion = "6.0.6"
    hibernateVersion = "5.2.10.Final"
    siteMeshVersion = "3.0.1"
    userAgentUtilsVersion = "1.20"

}

dependencies {
    compile(
            //  javax servlet api
            "javax.servlet:javax.servlet-api:$javaxServletAPIVersion",
            //  ------------------------------

            //  lombok
            "org.projectlombok:lombok:1.16.16",
            //  ------------------------------

            //  Spring framework
            "org.springframework:spring-core:$springVersion",
            "org.springframework:spring-web:$springVersion",
            //  ------------------------------

            //  Srping JPA
            "org.springframework.data:spring-data-jpa:$springJpaVersion",
            "org.hibernate:hibernate-entitymanager:$hibernateVersion",
            //  ------------------------------

            //  apsectj
            "org.aspectj:aspectjweaver:$aspectjVersion",
            //  ------------------------------

            //  log4j
            "org.apache.logging.log4j:log4j-api:$log4jVersion",
            //  ------------------------------

            // jackjosn 用于把javabean转换成json
            "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion",

            // fastjson 用于把javabean转换成json
            "com.alibaba:fastjson:$fastJsonVersion",
            //  ------------------------------

            // org.apache.commons
            "org.apache.commons:commons-configuration2:2.1.1",
            //  ------------------------------

            //  解析user-agent
            "eu.bitwalker:UserAgentUtils:$userAgentUtilsVersion",
            //  ------------------------------

            // org.apache.oro
            "oro:oro:$apacheOroVersion"
            //  ------------------------------

    )

    testCompile group: 'junit', name: 'junit', version: '4.12'
}


// 这个是配置 bintrayUpload
bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'boracay'
        userOrg = user
        licenses = ['Apache-2.0']
        vcsUrl = gitUrl
        version {
            name = "1.0.1"
            desc =  projectDescription
            vcsTag = 'master'
            attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
        }
    }
}
// 配置结束


install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'jar'

                // Add your description here
                name projectName
                description projectDescription
                url "https://github.com/roamerxv/boracay"

                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'roamerxv'        //填写bintray或者github的用户名
                        name '徐泽宇'         //姓名，可以是中文
                        email 'roamerxv@gmail.com'
                    }
                }

                scm {
                    connection gitUrl
                    developerConnection sourceUrl
                    url sourceUrl
                }
            }
        }
    }
}


// java doc
javadoc {
    options {
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version true
        links "http://docs.oracle.com/javase/8/docs/api"
    }
}
